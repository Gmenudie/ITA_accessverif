/* -- eval.y --       
   Evaluation d'une expression       
   Partie analyseur grammatical.    

   Utilisation : bison eval.y */

%{
#include <stdio.h>
#include "Automate.h"


/* Pré-déclarations */

extern int yylex();
extern FILE * yyin;
void yyerror (Automate * , const char *);

/* Variable définie dans un autre fichier */
extern char* yytext;


%}

%defines /* Génère le fichier en-tête eval.tab.h */

%code requires { 
	#include <ppl.hh>
	#include <list>
	#include "Automate.h"
	#include "Etat.h"
	#include "Transition.h"
	#include "Assignement.h"

	using namespace Parma_Polyhedra_Library;
	using namespace std; 
}

%parse-param {Automate *automate}

%start  elements /* Axiome de la grammaire */

%union{
  Constraint* c;
  Constraint_System* cs;
  Linear_Expression* e;
  int i;
  char* str;
  NNC_Polyhedron* p;
  Variable* v;
  Assignement* a;
  list<Assignement>* la;
}


%token<i> ENTIER
%token<str> NOM
%token<i> VAR

%token MAX_LEVEL 		
%token STATE			
%token INI			
%token FINAL			
%token TRANSITION		
%token GUARD		
%token ASSIGNMENT	
%token OBRACE
%token EBRACE
%token MINEQUAL
%token SUPEQUAL
%token LEVEL

%type<p> conditions
%type<c> contrainte
%type<cs> contraintes
%type<a> formule
%type<la> assignements formules
%type<e> facteur expr terme


%%

elements   : element              
           | elements element   
           ;

element   : etat
          | transition
          | taille
          ;

taille   : MAX_LEVEL ENTIER            
	{
		automate->setDimensions($2);
		std::list<Variable> variables;
        for (int i=0; i!=$2; i++){
            Variable A(i);
            variables.push_back(A);            
        }
        automate->setVariables(variables);

	}              
         ;



etat     : STATE NOM LEVEL ENTIER           {cout << "Trouvé un Etat, nom: " << $2 << " , niveau " << $4; automate->addEtat(Etat($2,$4));}
    		 | INI STATE NOM LEVEL ENTIER	 {cout << "Trouvé un Etat, nom: " << $3 << " , niveau " << $5; automate->addEtatInitial(Etat($3,$5));}
    	 	 | FINAL STATE NOM LEVEL ENTIER	 {cout << "Trouvé un Etat, nom: " << $3 << " , niveau " << $5; automate->addEtatFinal(Etat($3,$5));}
         ;



transition  : TRANSITION OBRACE NOM NOM conditions assignements EBRACE  
  {
  	//Façon assez moche de procéder mais plutôt optimale. On passe une transition semi-complète à la classe automate qui se charge de la compléter.
  	cout << "\nCréation d'une transition\n";
  	Transition* transition = new Transition;
  	cout << "Créée.\nAjout des conditions\n";
  	transition->setConditions(*$5);
  	cout << "\nFait.\nAjout des assignements\n";
  	transition->setAssignements(*$6);
  	cout << "\nFait.\n Ajout à l'automate\n";
  	automate->addTransition( transition, $3, $4 );
  	cout << "Fait.\n";
  }

            | TRANSITION OBRACE NOM NOM conditions EBRACE
  {
  	//Idem
  	Transition* transition  = new Transition;
  	transition->setConditions(*$5);
  	automate->addTransition( transition, $3, $4 );
  }
            ;

conditions  : GUARD contraintes   {$$ = new NNC_Polyhedron (*$2);}
            ;

contraintes : contrainte     			{$$ = new Constraint_System (*$1) ;}
            | contraintes contrainte    {$1->insert(*$2) ;}
            ;

contrainte  : expr '=' expr ';'     	{$$ = new Constraint(*$1 == *$3);}
            | expr '<' expr ';'     	{$$ = new Constraint(*$1 < *$3);}
            | expr '>' expr ';'     	{$$ = new Constraint(*$1 > *$3);}
            | expr MINEQUAL expr ';'    {$$ = new Constraint(*$1 <= *$3);}
            | expr SUPEQUAL expr ';'    {$$ = new Constraint(*$1 >= *$3);}
            ;

assignements : ASSIGNMENT formules  {$$=$2;}
             ;

formules  : formule    				{ list<Assignement>* assignements = new list<Assignement>;
            										assignements->push_back(*$1);
            										$$ = assignements;
            									}
          | formules formule   		{ $1->push_back(*$2) ;}
          ;

formule   : VAR '=' expr ';'		{$$ = new Assignement(Variable($1), *$3);}	
		  ;

expr      : expr '+' facteur		{$$ = new Linear_Expression(*$1 + *$3);}
          | expr '-' facteur		{$$ = new Linear_Expression(*$1 - *$3);}
          | facteur					{$$ = $1;}
          ;

facteur   : ENTIER '*' terme		{$$ = new Linear_Expression($1 * (*$3));}
          | ENTIER					{$$ = new Linear_Expression($1);}
          | VAR  					{$$ = new Linear_Expression(Variable($1));}
          ;

terme     : ENTIER					{$$ = new Linear_Expression($1);}
		  | VAR    					{$$ = new Linear_Expression(Variable($1));}
   		  ;





%%
#include <stdio.h>
#include "eval.tab.hpp"

void yyerror (Automate* automate,const char * error) 
{
    fprintf (stderr, "Erreur: %s sur l'expression %s\n", error, yytext);
}


